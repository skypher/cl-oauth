
(in-package :oauth)

(export '(parameter))

(defun parameter-from-http-auth (name &optional (request *request*))
  nil) ; stub; not supported yet.
  
(defun parameter-from-post-data (name &optional (request *request*))
  (hunchentoot:post-parameter name))

(defun parameter-from-get-data (name &optional (request *request*))
  (hunchentoot:get-parameter name))

(defun parameter (name &optional (request *request*))
  "Get the parameter NAME from REQUEST. See section 5.2."
  ;; TODO: check that the parameter appears only once.
  (or (parameter-from-http-auth name request)
      (parameter-from-post-data name request)
      (parameter-from-get-data name request)))

(defun parameters (&optional (request *request*))
  (append nil ; TODO: http auth header parameters
    (hunchentoot:post-parameters request)
    (hunchentoot:get-parameters request)))

(defun normalized-parameters (&key (request *request*)
                                   (auth-parameters-fn (constantly nil))
                                   (post-parameters-fn #'hunchentoot:post-parameters)
                                   (get-parameters-fn #'hunchentoot:get-parameters))
  "Collect request parameters and remove those excluded by the standard. See 9.1.1."
  (let ((parameters (remove "oauth_signature"
                            (append (remove "realm" (funcall auth-parameters-fn request)
                                            :key #'car :test #'equalp) ; TODO: http auth header parameters
                                    (funcall post-parameters-fn request)
                                    (funcall get-parameters-fn request))
                            :key #'car :test #'equalp)))
    (sort parameters #'string< :key (lambda (x)
                                      "Sort by key and value."
                                      (concatenate 'string (princ-to-string (car x))
                                                           (princ-to-string (cdr x)))))))

(defun splice-alist (alist)
  (reduce #'nconc (mapcar (lambda (x)
                            (list (car x) (cdr x)))
                          alist)))

(defun alist->query-string (alist &key (include-leading-ampersand t))
    (let ((result (format nil "两窿箴扉沐犰轶犰轶舂┅篚怏羼蝈篚祠ㄩ矧弪镳戾铉翳蝈篚祠┅轭沆蹁瀛戾徜轭绛犴疱蝮犷洎暴┅ㄤ彐躅聃弪篝蜷铉踞扉篝聃弪篝蜷铉戾舄è膂疳轵蝈盹鲥箴扉舡箦聃孱沐＼聃弪篝蜷铉呼弩＇羼踽飑ㄡ扉篝磲疸狎灬礅溽膂疳轵戾è膂箴扉舡箦聃孱沐＼膂疳轵┅ㄣ镱ㄦ轵篝膂箦泔钿膂┅┅膂疳轵螬┅犰轶舂ㄤ彐礤翳镤铒蝽犰辁瀛蝈聃弩舡躜è躜篝蜷铉┅铒蝽犰辁瀛蝈聃弩舡躜瘐蜷吼狎箦躜躜椹┅ㄤ彐礤翳镤铒蝽犰辁瀛蝈聃弩舡躜è躜瘐蜷乎蜷┅⒐碑并戾è痱轭舡汜箦轰秣钽狍濠鲥蜷纟翳狒翳轶黠螂蟆ㄣ镱汜翦钺翦篝蜷铉痱轭悱麸篝蜷铉瘐蜷乎蜷筱桢礤躜椹⒑瘐蜷乎蜷栾篝躜椹瘐蜷乎蜷疳翳躜椹┅ㄤ彐躅箝珙狒躜瀛忉箦篝蜷铉躜脲蝈聃弩蝈聃弩舄礤翳镤蝈聃弩舡礤翳镤蝈聃弩舂疳蜥礤翦蝮铒蝽犰辁邃疳蜥礤翦蝮候羼蹂篝蝈聃弩舂┅戾è痱轭舡汜箦轰秣钽狍濠ㄣ镱汜翦钺翦篝蜷铉痱轭悱麸篝蜷铉礤翳镤Βㄨ躅汨孱麸雉乎蜢孱泔溴铒蝽犰辁瀛蝈聃弩舡躜躜椹Βㄨ躅汨孱麸雉乎蜢孱泔溴ㄡ扉篝抉蹂蝙篝蜷铉疳蜥礤翦蝮洪钽祯溴戾徜轭绛犴疱蝮犷铋飑┅┅ㄤ彐躅桧徙脲ㄣ镱篚礤颦箦泸弭麸脲瞽箦泸弭⒐并ㄣ镱汜翦钺翦篝蜷铉泔铙蹴弪箦泸弭Β麸脲瞽箦泸弭┅ㄤ彐躅孱泔溴箝珙狒躜镢翦趔⒐伯雹ㄨ躅汨孱麸雉乎蜢孱泔溴ㄣ飙忉箦洞乎筲腑狎蜥麸忉箦洞篝蜷铉镢翦趔┅